
=encoding utf8

=head1 NAME

Mojolicious::Guides::Rendering - Rendering

=head1 OVERVIEW

Generating content with the L<Mojolicious> renderer.

=head1 CONCEPTS

Essentials every L<Mojolicious> developer should know.

=head2 Renderer

The renderer is a tiny black box turning stash data into actual responses
utilizing multiple template systems and data encoding modules.

  {text => 'Hello!'}                 -> 200 OK, text/html, 'Hello!'
  {json => {x => 3}}                 -> 200 OK, application/json, '{"x":3}'
  {text => 'Oops!', status => '410'} -> 410 Gone, text/html, 'Oops!'

Templates can be automatically detected if enough information is provided by
the developer or routes.
Template names are expected to follow the C<name.format.handler> scheme, with
C<name> defaulting to C<controller/action> or the route name, C<format>
defaulting to C<html> and C<handler> to C<ep>.

  {controller => 'users', action => 'list'} -> 'users/list.html.ep'
  {name => 'foo', format => 'txt'}          -> 'foo.txt.ep'
  {name => 'foo', handler => 'epl'}         -> 'foo.html.epl'

All templates should be in the C<templates> directory of the application
or the C<DATA> section of the class C<main>.

  __DATA__
  @@ time.html.ep
  <!doctype html><html>
    <head><title>Time</title></head>
    <body><%= localtime time %></body>
  </html>

  @@ hello.txt.ep
  ...

The renderer can be easily extended to support additional template systems
with plugins, but more about that later.

=head2 Embedded Perl

L<Mojolicious> includes a minimalistic but very powerful template system out
of the box called Embedded Perl or C<ep> for short.
It allows the embedding of Perl code right into actual content using a small
set of special tags and line start characters.

  <% Inline Perl %>
  <%= Perl expression, replaced with XML escaped result %>
  <%== Perl expression, replaced with raw result %>
  <%# Comment, useful for debugging %>
  % Perl line
  %= Perl expression line, replaced with XML escaped result
  %== Perl expression line, replaced with raw result
  %# Comment line, useful for debugging

The simplest form is used to insert raw Perl code.
Tags and lines work pretty much the same, but depending on context one will
usually look a bit better.

  <% my $i = 10; %>
  Text before.
  <% for my $j (1 .. $i) { %>
    <%= $j %>
  <% } %>
  Text after.

  % my $i = 10;
  Text before.
  % for my $j (1 .. $i) {
    %= $j
  % }
  Text after.

You can also insert results from actual Perl code using expressions, by
default the characters C<E<lt>>, C<E<gt>>, C<&>, C<'> and C<"> will be
escaped to prevent XSS attacks against your application.
Semicolons get automatically appended to all expressions.

  <%= 'lalala' %>
  <%== '<p>test</p>' %>

Only L<Mojo::ByteStream> objects are excluded from automatic escaping.

  <%= b('<p>test</p>') %>

You can also add an additional equal sign to the end of a tag to have it
automatically remove all surrounding whitespace, this allows free indenting
without ruining the result.

  <% for (1 .. 3) { %>
    <%= $foo =%>
  <% } %>

Comment tags and lines are very useful to deactivate code for debugging
purposes.

  % my $foo = 'lalala';
  <%# if ($foo) { %>
    <%= $foo =%>
  <%# } %>

Stash values that don't have invalid characters in their name get
automatically initialized as normal variables in the template and the
controller instance as C<$self>.

  $r->route('/foo/:name')
    ->to(controller => 'foo', action => 'bar', name => 'tester');

  Hello <%= $name %>.

There are also many built in helper functions such as C<url_for>, which
allows you to generate the URL of a specific route just from its name.

  $r->route('/foo/:name')->to('foo#bar')->name('some_route');

  <%= url_for 'some_route' %>

=head1 BASICS

Most commonly used features every L<Mojolicious> developer should know about.

=head2 Automatic Rendering

The renderer can be manually started by calling the C<render> controller
method, but thats usually not necessary, because it will get automatically
called if nothing has been rendered after the routes dispatcher finished its
work.
This also means you can have routes pointing only to templates without actual
actions.

  $self->render;

There is one big difference though, by calling C<render> manually you can
make sure that templates use the current controller instance and not the
default controller specified in the C<controller_class> attribute of the
application class.

=head2 Rendering Templates (C<template>)

The renderer will always try to detect the right template but you can also
use the C<template> stash value to render a specific one.

  $self->render(template => 'foo');

Choosing a specific C<format> and C<handler> is just as easy.

  $self->render(template => 'foo', format => 'txt', handler => 'epl');

Because rendering a specific template is the most common task it also has a
shortcut.

  $self->render('foo');

All values passed to the C<render> call are only temporarily assigned to the
stash and get reset again once rendering is finished.

=head2 Rendering Inline Templates (C<inline>)

Some renderers such as C<ep> allow templates to be passed inline.

  $self->render(inline => 'The result is <%= 1 + 1%>!');

Since auto detection depends on a path you might have to supply a C<handler>
too.

  $self->render(inline => "<%= shift->param('foo') %>", handler => 'epl');

=head2 Rendering Text (C<text>)

Perl characters can be rendered with the C<text> stash value, the given
content will be automatically encoded to bytes.

  $self->render(text => 'Hello WÃ¶rld!');

=head2 Rendering Data (C<data>)

Raw bytes can be rendered with the C<data> stash value, no encoding will be
performed.

  $self->render(data => $octets);

=head2 Rendering JSON (C<json>)

The C<json> stash value allows you to pass Perl structures to the renderer
which get directly encoded to JSON.

  $self->render(json => {foo => [1, 'test', 3]});

=head2 Partial Rendering (C<partial>)

Sometimes you might want to access the rendered result, for example to
generate emails, this can be done using the C<partial> stash value.

  my $html = $self->render('mail', partial => 1);

=head2 Status Code (C<status>)

Response status codes can be changed with the C<status> stash value.

  $self->render(text => 'Oops!', status => 500);

=head2 Content Type (C<format>)

The C<Content-Type> header of the response is actually based on the MIME type
mapping of the C<format> stash value.

  $self->render(text => 'Hello!', format => 'txt');

These mappings can be easily extended or changed.

  # Application
  package MyApp;
  use Mojo::Base 'Mojolicious';

  sub startup {
    my $self = shift;

    # Add new MIME type
    $self->types->type(txt => 'text/plain; charset=utf-8');
  }

  1;

=head2 Stash Data

Data can be passed to templates through the C<stash> in any of the native
Perl data types.

  $self->stash(author => 'Sebastian');
  $self->stash(frameworks => [qw/Catalyst Mojolicious/]);
  $self->stash(examples => {tweetylicious => 'a microblogging app'});

  <%= $author %>
  <%= $frameworks->[1] %>
  <%= $examples->{tweetylicious} %>

Since everything is just Perl normal control structures just work.

  <% for my $framework (@$frameworks) { %>
    <%= $framework %> was written by <%= $author %>.
  <% } %>

  <% while (my ($app, $description) = each %$examples) { %>
    <%= $app %> is a <%= $description %>.
  <% } %>

=head2 Helpers

Helpers are little functions you can use in templates and controller code.

  <%= dumper [1, 2, 3] %>

  my $serialized = $self->dumper([1, 2, 3]);

The C<dumper> helper for example will use L<Data::Dumper> to serialize
whatever data structure you pass it, this can be very useful for debugging.
We differentiate between C<default helpers> which are more general purpose
like C<dumper> and C<tag helpers>, which are template specific and mostly
used to generate C<HTML> tags.

  <%= javascript '/script.js' %>

  <%= javascript begin %>
    var a = 'b';
  <% end %>

The plugins L<Mojolicious::Plugin::DefaultHelpers> and
L<Mojolicious::Plugin::TagHelpers> contain all of them.

=head2 Layouts

Most of the time when using C<ep> templates you will want to wrap your
generated content in a HTML skeleton, thanks to layouts thats absolutely
trivial.

  @@ foo/bar.html.ep
  % layout 'mylayout';
  Hello World!

  @@ layouts/mylayout.html.ep
  <!doctype html><html>
    <head><title>MyApp!</title></head>
    <body><%= content %></body>
  </html>

You just select the right layout template with the C<layout> helper and place
the result of the current template with the C<content> helper.
You can also pass along normal stash values to the C<layout> helper.

  @@ foo/bar.html.ep
  % layout 'mylayout', title => 'Hi there!';
  Hello World!

  @@ layouts/mylayout.html.ep
  <!doctype html><html>
    <head><title><%= $title %></title></head>
    <body><%= content %></body>
  </html>

=head2 Including Partial Templates

Like most helpers the C<include> helper is just a shortcut to make your life
a little easier.

  @@ foo/bar.html.ep
  <!doctype html><html>
    <%= include 'header' %>
    <body>Bar!</body>
  </html>

  @@ header.html.ep
  <head><title>Howdy!</title></head>

Instead of C<include> you could also just call C<render> with the C<partial>
argument.

  @@ foo/bar.html.ep
  <!doctype html><html>
    <%= $self->render('header', partial => 1) %>
    <body>Bar!</body>
  </html>

  @@ header.html.ep
  <head><title>Howdy!</title></head>

Of course you can also pass stash values.

  @@ foo/bar.html.ep
  <!doctype html><html>
    <%= include 'header', title => 'Hello!' %>
    <body>Bar!</body>
  </html>

  @@ header.html.ep
  <head><title><%= $title %></title></head>

=head2 Reusable Template Blocks

It's never fun to repeat yourself, thats why you can build reusable template
blocks in C<ep> that work very similar normal Perl functions.

  <% my $block = begin %>
    <% my $name = shift; %>
    Hello <%= $name %>.
  <% end %>
  <%= $block->('Sebastian') %>
  <%= $block->('Sara') %>

Blocks are always delimited by the C<begin> and C<end> keywords.

  % my $block = begin
    % my $name = shift;
    Hello <%= $name %>.
  % end
  % for (1 .. 10) {
    %= $block->('Sebastian')
  % }

A naive translation to equivalent Perl code could look like this.

  my $output = '';
  my $block  = sub {
    my $name   = shift;
    my $output = '';
    $output .= "Hello $name.";
    return $output;
  }
  for (1 .. 10) {
    $output .= $block->('Sebastian');
  }
  print $output;

=head2 Content Blocks

Blocks and the C<content_for> helper can also be used to pass whole sections
of the template to the layout.

  @@ foo/bar.html.ep
  % layout 'mylayout';
  <% content_for header => begin %>
    <title>MyApp!</title>
  <% end %>
  Hello World!
  <% content_for header => begin %>
    <meta http-equiv="Pragma" content="no-cache">
  <% end %>

  @@ layouts/mylayout.html.ep
  <!doctype html><html>
    <head><%= content_for 'header' %></head>
    <body><%= content %></body>
  </html>

=head2 Template Inheritance

Inheritance takes the layout concept above one step further.
Unlike C<content_for> the C<content> helper does not allow appending to
existing values, this makes it possible to overload whole template sections.
The only difference between C<layout> and the C<extends> is that extended
templates don't get prefixed with C<layouts/>.

  @@ first.html.ep
  %# "<div>First header!First footer!</div>"
  <div>
  <%= content header => begin %>
    First header!
  <% end %>
  <%= content footer => begin %>
    First footer!
  <% end %>
  </div>

  @@ second.html.ep
  %# "<div>Second header!First footer!</div>"
  % extends 'first';
  <% content header => begin %>
    Second header!
  <% end %>

  @@ third.html.ep
  %# "<div>Second header!Third footer!</div>"
  % extends 'second';
  <% content footer => begin %>
    Third footer!
  <% end %>

This chain could go on and on to allow a very high level of template reuse.

=head2 Memorizing Template Blocks

Compiled templates are always cached in memory, but with the C<memorize>
helper you can go one step further and prevent template blocks from being
executed more than once.

  <%= memorize begin %>
    This template was compiled at <%= localtime time %>.
  <% end %>

=head2 Mode Specific C<exception> And C<not_found> Templates

While the built in C<exception> and C<not_found> templates are very useful
during development, you most likely want to show your users something more
related to your application in production.
That's why L<Mojolicious> will always try to render C<exception.$mode.html.*>
or C<not_found.$mode.html.*> before falling back to the built in default
templates.

  @@ exception.production.html.ep
  <!doctype html><html>
    <head><title>Server Error</title></head>
    <body>Something bad happened!</body>
  </html>

  @@ not_found.production.html.ep
  <!doctype html><html>
    <head><title>Page Not Found</title></head>
    <body>Page does not seem to exist.</body>
  </html>

=head1 ADVANCED

Less commonly used and more powerful features.

=head2 Chunked Transfer Encoding

For very dynamic content you might not know the response C<Content-Length>
in advance, thats where the C<chunked> C<Transfer-Encoding> comes in handy.
A common use would be to send the C<head> section of an HTML document to the
browser in advance and speed up preloading of referenced images and
stylesheets.

  $self->write_chunk('<html><head><title>Example</title>');
  $self->write_chunk('<link href="example.css" rel="stylesheet"');
  $self->write_chunk(' type="text/css"></head>', sub {
    my $self = shift;
    $self->write_chunk('<body>Example</body></html>');
    $self->write_chunk('');
  });

The optional drain callback ensures that all previous chunks have been
written before processing continues.
An empty chunk marks the end of the stream.

  22
  <html><head><title>Example</title>
  29
  <link href="example.css" rel="stylesheet"
  17
   type="text/css"></head>
  1C
  <body>Example</body></html>
  0

Especially in combination with long connection timeouts this can be very
useful for Comet (C<long polling>).
Due to limitations in some web servers this might not work perfectly in all
deployment environments.

=head2 Encoding

Templates stored in files are expected to be C<UTF-8> by default, but that
can be easily changed.

  # Application
  package MyApp;
  use Mojo::Base 'Mojolicious';

  sub startup {
    my $self = shift;

    # Different encoding
    $self->renderer->encoding('koi8-r');
  }

  1;

All templates from the DATA section are bound to the encoding of the Perl
script, so don't forget to use the L<utf8> pragma if necessary.

=head2 Base64 Encoded DATA Files

Base64 encoded static files such as images can be easily stored in the
C<DATA> section of your application, similar to templates.

  @@ favicon.ico (base64)
  ...base64 encoded image...

=head2 Inflating DATA Templates

Templates stored in files get preferred over files from the C<DATA> section,
this allows you to include a default set of templates in your application
that the user can later customize.
The C<inflate> command will write all templates and static files from the
C<DATA> section into actual files in the C<templates> and C<public>
directories.

  % ./myapp.pl inflate
  ...

=head2 Customizing The Template Syntax

You can easily change the whole template syntax by loading the C<ep_renderer>
plugin with a custom configuration.

  use Mojolicious::Lite;

  plugin ep_renderer => {
    name     => 'mustache',
    template => {
      tag_start => '{{',
      tag_end   => '}}'
    }
  };

  get '/' => 'index';

  app->start;
  __DATA__

  @@ index.html.mustache
  Hello {{= $name }}.

L<Mojo::Template> contains the whole list of available options.

=head2 Adding Helpers

Adding and redefining helpers is very easy, you can use them to do pretty
much everything.

  use Mojolicious::Lite;

  app->helper(
    debug => sub {
      my ($self, $string) = @_;
      $self->app->log->debug($string);
    }
  );

  get '/' => sub {
    my $self = shift;
    $self->debug('action');
  } => 'index';

  app->start;
  __DATA__

  @@ index.html.ep
  % debug 'template';

Helpers can also accept template blocks as last argument, this for example
allows very pleasant to use tag helpers and filters.

  use Mojolicious::Lite;
  use Mojo::ByteStream;

  app->helper(
    trim_newline => sub {
      my ($self, $block) = @_;
      my $result = $block->();
      $result =~ s/\n//g;
      return Mojo::ByteStream->new($result);
    }
  );

  get '/' => 'index';

  app->start;
  __DATA__

  @@ index.html.ep
  <%= trim_newline begin %>
    Some text.
    <%= 1 + 1 %>
    More text.
  <% end %>

Wrapping the helper result into a L<Mojo::ByteStream> object can prevent
accidental double escaping.

=head2 Adding Your Favorite Template System

Maybe you would prefer a different template system than C<ep>, all you have
to do is add a new C<handler>.

  use Mojolicious::Lite;

  app->renderer->add_handler(
    mine => sub {
      my ($r, $c, $output, $options) = @_;

      # One time use inline template
      my $inline = $options->{inline};

      # Generate relative template path
      my $name = $r->template_name($options);

      # Try to find appropriate template in DATA section
      my $content = $r->get_inline_template($options, $name);

      # Generate absolute template path
      my $path = $r->template_path($options);

      # This part is up to you and your template system :)
      ...

      # Pass the rendered result back to the renderer
      $$output = 'The rendered result!';
    }
  );

  get '/' => 'index';

  app->start;
  __DATA__

  @@ index.html.mine
  ...

Since most template systems don't support templates in the C<DATA> section
the renderer provides methods to help you with that.

=head1 MORE

You can continue with L<Mojolicious::Guides> now or take a look at the
Mojolicious wiki L<http://github.com/kraih/mojo/wiki>, which contains a lot
more documentation and examples by many different authors.

=cut
